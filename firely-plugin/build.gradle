/*
 * Copyright 2017 Busbud
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy of this software and
 * associated documentation files (the "Software"), to deal in the Software without restriction,
 * including without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense,
 * and/or sell copies of the Software, and to permit persons to whom the Software is furnished to do
 * so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all copies or substantial
 * portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED,
 * INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A
 * PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR
 * COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN
 * ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH
 * THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 */

plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'com.gradle.plugin-publish' version '1.3.0'
}

dependencies {
    implementation gradleApi()
    implementation 'com.squareup:kotlinpoet:1.11.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.9.0'
    compileOnly 'com.android.tools.build:gradle:8.10.1'
}

tasks.withType(JavaCompile).configureEach {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

tasks.withType(GroovyCompile).configureEach {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

kotlin {
    jvmToolchain(17)
}

tasks.register('sourcesJar', Jar) {
    dependsOn classes
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

tasks.withType(Copy).configureEach { duplicatesStrategy = DuplicatesStrategy.EXCLUDE }

artifacts {
    archives sourcesJar
}

version = "$lib_version"
group = "com.busbud"


gradlePlugin {
    website = 'https://github.com/busbud/firely'
    vcsUrl = 'https://github.com/busbud/firely'
    plugins {
        firelyPlugin {
            id = 'com.busbud.android.firely'
            implementationClass = 'com.busbud.android.firely.FirelyPlugin'
            displayName = "Firely Plugin"
            description = "Library to simplify the integration and management of A/B testing"
            tags = ['firely', 'remote_configs', 'firebase']
        }
    }
}

publishing {
    publications {
        main(MavenPublication) {
            groupId 'com.busbud.android'
            artifactId 'firely-plugin'
            version "$lib_version"

            from components.java
        }
    }
}

